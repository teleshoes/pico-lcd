#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename dirname);
use Time::HiRes qw(time);

sub rshellInstallFiles(@);
sub nowMillis();
sub run(@);
sub tryrun(@);

my $RSHELL_EXEC = dirname($0) . "/rshell";

my @PY_SRC_FILES_RAW = qw(
  src/main.py
);
my @PY_SRC_FILES_MPY = qw(
  src/app.py
  src/doc.py
  src/font_generator.py
  src/lcdFont.py
  src/lcd.py
  src/rtc.py
);

my @PY_SRC_FILES_ALL = (
  @PY_SRC_FILES_RAW,
  map {my $mpy=$_; $mpy =~ s/^src/mpy/; $mpy =~ s/\.py$/\.mpy/; $mpy} @PY_SRC_FILES_MPY,
);

my @STATE_FILES = qw(
  state-wifi-conf
);

#DS3231 rtc can only handle 2 centuries anyway
my $TZ_START_YEAR = 1900;
my $TZ_END_YEAR = 2100;

my @TZ_ZONENAMES = qw(
  America/New_York
);

my $EXEC = basename $0;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS]
  $EXEC [OPTS} conf | --conf
    same as: $EXEC --timezone && $EXEC --state

  $EXEC [OPTS] tz | timezone | zoneinfo | --tz | --timezone | --zoneinfo
    -calculate tzdata CSV with zoneinfo-tool and put in zoneinfo/ dir for:
      @TZ_ZONENAMES
    -copy files to /pyboard with `rshell rsync`:
      zoneinfo/

  $EXEC [OPTS] state | --state
    -copy OPTIONALLY PRESENT files to /pyboard with rshell:
      @STATE_FILES

  $EXEC [OPTS] [BOARD] mpy | compile | --mpy | --compile
    -compile python files with mpy-cross:
      @PY_SRC_FILES_MPY
    -copy files to /pyboard with `rshell rsync`:" .
      join('', map {"      $_\n"} @PY_SRC_FILES_ALL) . "

  BOARD
    one of:
      pico  | --pico
        use 'mpy-cross -march=armv6m'
      pico2 | --pico2
        use 'mpy-cross -march=armv7m'

  OPTS
    -d | --delete
      delete all *.py and *.mpy files on /pyboard before installing
";

my $MODE_TIMEZONE = "timezone";
my $MODE_STATE = "state";
my $MODE_MPY = "mpy";

sub main(@){
  my $opts = {
    delete       => 0,
  };
  my @modes = ($MODE_TIMEZONE, $MODE_STATE);
  my $board = undef;
  while(@_ > 0){
    my $arg = shift;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(--)?(conf)$/){
      @modes = ($MODE_TIMEZONE, $MODE_STATE);
    }elsif($arg =~ /^(--)?(tz|timezone|zoneinfo)$/){
      @modes = ($MODE_TIMEZONE);
    }elsif($arg =~ /^(--)?(state)$/){
      @modes = ($MODE_STATE);
    }elsif($arg =~ /^(--)?(mpy|compile)$/){
      @modes = ($MODE_MPY);
    }elsif($arg =~ /^(pico|--pico|armv6m|--armv6m)$/){
      $board = "pico";
    }elsif($arg =~ /^(pico2|--pico2|armv7m|--armv7m)$/){
      $board = "pico2";
    }elsif($arg =~ /^(-d|--delete)$/){
      $$opts{delete} = 1;
    }else{
      die "$USAGE\nERROR: unknown arg $arg\n";
    }
  }

  if($$opts{delete}){
    tryrun "pkill -9 rshell";
    tryrun $RSHELL_EXEC, "rm -f /pyboard/*.mpy; rm /pyboard/*.py";
  }

  my @files;

  my %okModes = map {$_ => 1} @modes;

  if(defined $okModes{$MODE_TIMEZONE}){
    #calculate+install tzdata CSV files
    run "./zoneinfo-tool",
      "-c", "$TZ_START_YEAR,$TZ_END_YEAR",
      "--skip-existing",
      @TZ_ZONENAMES,
    ;
    push @files, "zoneinfo/";
  }

  if(defined $okModes{$MODE_STATE}){
    for my $file(@STATE_FILES){
      if(-f $file){
        push @files, $file;
      }else{
        print STDERR "\n\n=====\nWARNING: missing $file\n=====\n\n\n";
      }
    }
  }

  if(defined $okModes{$MODE_MPY}){
    die "ERROR: missing BOARD (e.g.: pico or pico2)\n" if not defined $board;

    run "rm", "-rf", "mpy/";
    run "mkdir", "mpy";
    my @mpyCrossCmd;
    if($board eq "pico"){
      @mpyCrossCmd = ("mpy-cross", "-march=armv6m");
    }elsif($board eq "pico2"){
      @mpyCrossCmd = ("mpy-cross", "-march=armv7m");
    }else{
      die "ERROR: unknown board $board\n";
    }

    for my $py(@PY_SRC_FILES_MPY){
      my $mpy = $py;
      $mpy =~ s/src\//mpy\//;
      $mpy =~ s/\.py$/.mpy/;
      run (@mpyCrossCmd, $py, "-o", $mpy);
      run "touch", $mpy, "-r", $py;
    }

    @files = (@files, @PY_SRC_FILES_ALL);
  }

  rshellInstallFiles(@files);
}

sub rshellInstallFiles(@){
  my (@files) = @_;
  my $pyboardTmpDir = "pyboard-tmp-" . nowMillis();
  run "mkdir", $pyboardTmpDir;
  for my $file(@files){
    run "cp", "-ar", $file, "$pyboardTmpDir/";
  }

  tryrun "pkill -9 rshell";
  run $RSHELL_EXEC, "rsync", "$pyboardTmpDir/", "/pyboard/";

  run "rm", "-rf", "$pyboardTmpDir/";
}

sub nowMillis(){
  return int(time() * 1000.0 + 0.5);
}

sub run(@){
  tryrun(@_);
  if($? != 0){
    die "ERROR: '@_' failed\n$!\n";
  }
}
sub tryrun(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
