#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);
use URI::Escape qw(uri_escape);

sub fetchWindowGeometry($);
sub calculateMaxFontSize($$$);
sub runCurlCmd($$$$$$);
sub parseConfig($);

my $EXEC = basename $0;

my $DEVICE_CONFIG_FILE = "$ENV{HOME}/.config/pico-lcd.conf";
my @DEVICES = parseConfig($DEVICE_CONFIG_FILE);
my @DEVICE_NAMES = map {$$_{devName}} @DEVICES;
my @DEVICE_SYNS = (map {@{$$_{syns}}} @DEVICES);
my %DEVICE_IPS_BY_NAME = map {
  my $ip = $$_{devIP}; map {$_ => $ip} ($$_{devName}, @{$$_{syns}})
} @DEVICES;
my $DEVICE_NAME_REGEX = join "|", (@DEVICE_NAMES, @DEVICE_SYNS);

my @DEFAULT_MARKUP_TEXT_PARAMS = qw(clear=true show=true info=true);

my $DEFAULT_LCD_WIDTH = 240;
my $DEFAULT_LCD_HEIGHT = 240;

my $FONT_BASE_HEIGHT = 8;
my $FONT_BASE_WIDTH = 5;

my $CLOCK_MARKUP = ""
  . "[size=4][hr][n]"
  . "[color=green][size=8][rtc=%H:%M][n]"
  . "[color=white][hr][size=4][n]"
  . " [rtc=%H:%M:%S]"
;

my $TEMPLATE_SETS = {
  "default" => {
    "timeout"        => "",
    "wifi-waiting"   => "",
    "wifi-connected" => "",
    "ap-waiting"     => "",
    "ap-active"      => "",
  },
  "clock" => {
    "timeout" => ""
      . "[size=4][rtc=%Y-%m-%d][n]"
      . $CLOCK_MARKUP,
    "wifi-waiting" => ""
      . "[size=2]WAITING WIFI   [rtc=%Y-%m-%d][n]"
      . "[var=ssid][n]"
      . $CLOCK_MARKUP,
    "wifi-connected" => ""
      . "[size=2]CONNECTED      [rtc=%Y-%m-%d][n]"
      . "[var=ip][n]"
      . $CLOCK_MARKUP,
    "ap-waiting" => ""
      . "[size=2]AP WAITING     [rtc=%Y-%m-%d][n]"
      . "[var=ssid][n]"
      . $CLOCK_MARKUP,
    "ap-active" => ""
      . "[size=2]ssid=[var=ssid]  [rtc=%Y-%m-%d][n]"
      . "pw=[var=password]   [var=ip][n]"
      . $CLOCK_MARKUP,
  },
};

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS] MARKUP_TEXT_ARG
    draw markup on lcd using '--cmd text', with default query params and OPTS
      MARKUP_TEXT_ARG = passed with --body, any string, cannot start with '-'
    same as: $EXEC --text MARKUP_TEXT_ARG

  $EXEC [OPTS] --text|text MARKUP_TEXT
    draw markup on lcd using '--cmd text', with default query params and OPTS
      MARKUP_TEXT_ARG = passed with --body, any string, cannot start with '-'
    -same as:
      $EXEC \\
        --prepend-size [OPTS] \\
        --cmd text \\
        @DEFAULT_MARKUP_TEXT_PARAMS \\
        MARKUP_TEXT \\
      ;

  $EXEC [OPTS] --template-set TEMPLATE_SET_NAME
    apply pre-configured set of markup templates
    TEMPLATE_SET_NAME = " . join(" | ", sort keys %$TEMPLATE_SETS) . "

  $EXEC [OPTS] --cmd|cmd CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
    -parse <IP_ADDRESS> for device from $DEVICE_CONFIG_FILE
       -see: --ip / --ap / --dev
    -run:
      curl 'http://<IP_ADDRESS>/<CMD>[<HTTP_PARAM_QUERY_STRING>]' \\
        [--data '<DATA_BODY>'] \\
        [--upload-file '<FILENAME>'] \\
      ;

        HTTP_PARAM_QUERY_STRING
          HTTP query string, empty if no PARAM_NAME=PARAM_VAL given, otherwise
            consisting of a '?' character, followed by all '<PARAM_NAME>=<PARAM_VAL>'
            params joined with '&' as separator
          e.g.: '?param1=val1&param2=val2&param3=val3'
        PARAM_NAME
          can ontain only letters, numbers, and underscores
        PARAM_VAL
          can be any string, percent-encoded with URI::Escape->uri_escape()
        DATA_BODY
          can be any string, see --data / --body
        FILENAME
          must be a file on the local filesystem, see --upload-file

  $EXEC [OPTS] --info|info
    fetch+show current state
    same as: $EXEC [OPTS] --cmd info

  $EXEC [OPTS] --connect|connect
    re-connect to wifi or setup access point
    same as: $EXEC [OPTS] --cmd connect

  $EXEC [OPTS] --ssid|ssid SSID PASSWORD [TIMEOUT]
    append a network for next boot/connect
      SSID     = network SSID
      PASSWORD = network password
      TIMEOUT  = network-specific max seconds to before trying next, default is 10s
    same as: $EXEC [OPTS] --cmd ssid ssid=SSID password=PASSWORD [timeout=TIMEOUT]

  $EXEC [OPTS] --resetwifi|resetwifi
    REMOVE all configured wifi network SSIDs for next boot/connect
    same as: $EXEC [OPTS] --cmd resetwifi

  $EXEC [OPTS] --template|template TEMPLATE_NAME [TEMPLATE_MARKUP]
    write state-template-<TEMPLATE_NAME> file, to use dynamic markup for various scenarios
      TEMPLATE_NAME   = timeout | wifi-waiting | wifi-connected | ap-waiting | ap-active
      TEMPLATE_MARKUP = markup string, if omitted, use the default markup for that template
    same as: $EXEC [OPTS] --cmd template name=TZDATA_ZONE_NAME [--data=TEMPLATE_MARKUP]

  $EXEC [OPTS] --timeout|timeout [TIMEOUT_MILLIS]
    write state-timeout file, to show timeout template after TIMEOUT_MILLIS of no socket data
      TIMEOUT_MILLIS = delay in millis before timeout, if omitted, never timeout, block forever
    same as: $EXEC [OPTS] --cmd timeout [timeoutMillis=TIMEOUT_MILLIS]

  $EXEC [OPTS] --timezone|timezone [TZDATA_ZONE_NAME]
    write state-timezone file, to select the tzdata CSV file to use with DS3231 rtc
      TZDATA_ZONE_NAME = tzdata csv file path under '/zoneinfo/' dir, remove file if omitted
    same as: $EXEC [OPTS] --cmd timezone [name=TZDATA_ZONE_NAME]

  $EXEC [OPTS] --rtc|rtc [RTC_EPOCH]
    set the hardware DS3231 RTC epoch to RTC_EPOCH, and read the current value
      RTC_EPOCH = unix time in seconds UTC, skip update if omitted and just read current value
    same as: $EXEC [OPTS] --cmd rtc [epoch=RTC_EPOCH]

  $EXEC [OPTS] --clear|clear
    clear the entire LCD memory and reset framebuf
    same as: $EXEC [OPTS] --cmd clear

  $EXEC [OPTS] --show|show
    show the framebuffer, if present
    same as: $EXEC [OPTS] --cmd show

  $EXEC [OPTS] --buttons|buttons
    fetch and print button press counts
    same as: $EXEC [OPTS] --cmd buttons

  $EXEC [OPTS] --fill|fill COLOR
    fill the LCD with COLOR
      COLOR = any supported color string, e.g.: 'black', 'red', '#FF00FF'
    same as: $EXEC [OPTS] --cmd fill color=COLOR

  $EXEC [OPTS] --lcd|lcd LCD_NAME
    write state-lcd-name file, to set the hardware LCD model to LCD_NAME
      LCD_NAME = one of: 1_3 2_0 2_8
    same as: $EXEC [OPTS] --cmd lcd name=LCD_NAME

  $EXEC [OPTS] --orient|orient ORIENT
    write state-orientation file, to set the screen orientation
      ORIENT = 0 | 90 | 180 | 270 | landscape | portrait | inverted-landscape | inverted-portrait
    same as: $EXEC [OPTS] --cmd orient orient=ORIENT

  $EXEC [OPTS] --framebuf|framebuf FRAMEBUF
    write state-framebuf file, to enable/disable/resize/move the in-memory raster buffer
      FRAMEBUF = off | WxH+X+Y | WxH | full | left | right | top | bottom | square
    same as: $EXEC [OPTS] --cmd framebuf framebuf=FRAMEBUF

  $EXEC [OPTS] --upload|upload FILE [DEST_FILENAME]
    write file to remote filesystem
      FILE          = the local path of file to upload
      DEST_FILENAME = the remote path to write to, defaults to FILE if omitted
    same as: $EXEC [OPTS] --cmd upload filename=DEST_FILENAME --upload-file=FILE

  $EXEC [OPTS] --delete|delete FILENAME
    delete remote file
      FILENAME = the remote path of the file to delete
    same as: $EXEC [OPTS] --cmd delete filename=FILENAME

  $EXEC [OPTS] --bootloader|bootloader
  $EXEC [OPTS] --bootsel|bootsel
    enter bootloader (bootsel mass storage mode), and set curl max-time = 3s to prevent hanging
    same as: $EXEC [OPTS] --cmd bootloader -m 3

  OPTS
    -q | --quiet
      do not print curl command messages

    -m CURL_MAX_TIME_FRAC_SECONDS
    --max-time CURL_MAX_TIME_FRAC_SECONDS
    --max-time=CURL_MAX_TIME_FRAC_SECONDS
      pass '--max-time CURL_MAX_TIME_FRAC_SECONDS' to all calls to curl

    --data DATA | --data=DATA
    --body DATA | --body=DATA
      for --cmd, pass in DATA as POST payload with curl (using '--data DATA')
    --upload-file=FILENAME
      for --cmd, pass in FILENAME contents as PUT payload with curl
      (using '--upload-file' FILENAME)

    --ip=IP_ADDRESS
      use IP_ADDRESS in calls to curl
    --ap
      use IP_ADDRESS=192.168.4.1
    DEV_NAME | --dev=DEV_NAME | --dev DEV_NAME
    DEV_NAME | --dev=DEV_NAME | --dev DEV_NAME
      -parse contents of $DEVICE_CONFIG_FILE
         -each entry is a single line that contains:
           <CONF_DEV_NAME>, <IP_ADDRESS>, and optional space-separated <SYN_NAME> list
         -each element is separated by a comma or equals sign
         -i.e.:
           CONF_DEV_NAME = IP_ADDRESS
           CONF_DEV_NAME, IP_ADDRESS
           CONF_DEV_NAME, IP_ADDRESS, [SYN_NAME SYN_NAME SYN_NAME...]
      -find the first line where DEV_NAME equals CONF_DEV_NAME or SYN_NAME
        -select the IP_ADDRESS for that entry
      -same as: --ip=IP_ADDRESS
        DEV_NAME
          one of: $DEVICE_NAME_REGEX
    --default-dev | --any-dev
      -select the IP_ADDRESS of the first CONF_DEV_NAME in $DEVICE_CONFIG_FILE
      (this is the default)

    --prepend-size
      when using cmd=text, calculate the max size for the markup and prepend the markup with:
        [size=MAX_FONT_SIZE]
      (this is the default when '--text' or 'MARKUP_TEXT_ARG' is given)
    --no-prepend-size
      never calculate max size or prepend [size] markup
      (this is the default for '--cmd text' is given)
";

my $MODE_CMD = "cmd";
my $MODE_TEMPLATE_SET = "template-set";

sub main(@){
  my $mode = $MODE_CMD;
  my $ipAddr = undef;
  my $devName = undef;
  my $opts = {
    quiet       => 0,
    curlMaxTime => undef,
  };

  my $templateSet = undef;

  my $cmd = undef;
  my @cmdParams;
  my $cmdData = undef;
  my $cmdFile = undef;
  my $isBodyPrependSize = undef;

  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-q|--quiet)$/){
      $$opts{quiet} = 1;
    }elsif($arg =~ /^(-m|--max-time)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $$opts{curlMaxTime} = shift @_;
    }elsif($arg =~ /^(?:--max-time)=(\d+|\d*\.\d+)$/){
      $$opts{curlMaxTime} = $1;
    }elsif($arg =~ /^--ip=(.+)$/){
      $ipAddr = $1
    }elsif($arg =~ /^(--ap)$/){
      $ipAddr = '192.168.4.1';
    }elsif($arg =~ /^--dev=(\w+)$/){
      $devName = $1;
    }elsif($arg =~ /^--dev$/ and @_ > 0 and $_[0] =~ /^(\w+)$/){
      $devName = shift @_;
    }elsif($arg =~ /^($DEVICE_NAME_REGEX)$/){
      $devName = $1;
    }elsif($arg =~ /^(--default-dev|--any-dev)$/){
      $devName = undef;
    }elsif($arg =~ /^(--prepend-size)$/){
      $isBodyPrependSize = 1;
    }elsif($arg =~ /^(--no-prepend-size)$/){
      $isBodyPrependSize = 0;
    }elsif($arg =~ /^(--text|text)$/ and @_ > 0 and not defined $cmd){
      $cmdData = shift @_;
      $cmd = "text";
      @cmdParams = (@DEFAULT_MARKUP_TEXT_PARAMS, @cmdParams);
      $isBodyPrependSize = 1 if not defined $isBodyPrependSize;
    }elsif($arg =~ /^(--template-set|template-set)$/ and @_ > 0){
      $mode = $MODE_TEMPLATE_SET;
      $templateSet = shift @_;
      if(not defined $$TEMPLATE_SETS{$templateSet}){
        die "ERROR: unknown template set $templateSet\n";
      }
    }elsif($arg =~ /^(--cmd|cmd)$/ and @_ > 0 and not defined $cmd){
      $cmd = shift @_;
      while(@_ > 0 and $_[0] =~ /^\w+=.+$/){
        push @cmdParams, shift @_;
      }
    }elsif($arg =~ /^(--data|--body)$/ and @_ > 0){
      $cmdData = shift @_;
    }elsif($arg =~ /^(?:--data|--body)=(.+)$/s){
      $cmdData = $1;
    }elsif($arg =~ /^--upload-file=(.+)$/){
      $cmdFile = $1;
    }elsif($arg =~ /^(--info|info)$/ and not defined $cmd){
      $cmd = "info";
    }elsif($arg =~ /^(--connect|connect)$/ and not defined $cmd){
      $cmd = "connect";
    }elsif($arg =~ /^(--ssid|ssid)$/ and @_ >= 2 and not defined $cmd){
      $cmd = "ssid";
      push @cmdParams, "ssid=" . shift @_;
      push @cmdParams, "password=" . shift @_;
      if(@_ > 0){
        push @cmdParams, "timeout=" . shift @_;
      }
    }elsif($arg =~ /^(--resetwifi|resetwifi)$/ and not defined $cmd){
      $cmd = "resetwifi";
    }elsif($arg =~ /^(--template|template)$/ and @_ >= 1 and not defined $cmd){
      $cmd = "template";
      push @cmdParams, "templateName=" . shift @_;
      if(@_ > 0){
        my $templateMarkup = shift @_;
        $cmdData = $templateMarkup;
      }
    }elsif($arg =~ /^(--timeout|timeout)$/ and not defined $cmd){
      $cmd = "timeout";
      if(@_ > 0){
        push @cmdParams, "timeoutMillis=" . shift @_;
      }
    }elsif($arg =~ /^(--timezone|timezone)$/ and not defined $cmd){
      $cmd = "timezone";
      if(@_ > 0){
        push @cmdParams, "name=" . shift @_;
      }
    }elsif($arg =~ /^(--rtc|rtc)$/ and not defined $cmd){
      $cmd = "rtc";
      if(@_ > 0){
        push @cmdParams, "epoch=" . shift @_;
      }
    }elsif($arg =~ /^(--clear|clear)$/ and not defined $cmd){
      $cmd = "clear";
    }elsif($arg =~ /^(--show|show)$/ and not defined $cmd){
      $cmd = "show";
    }elsif($arg =~ /^(--buttons|buttons)$/ and not defined $cmd){
      $cmd = "buttons";
    }elsif($arg =~ /^(--fill|fill)$/ and @_ > 0 and not defined $cmd){
      $cmd = "fill";
      push @cmdParams, "color=" . shift @_;
    }elsif($arg =~ /^(--lcd|lcd)$/ and @_ > 0 and not defined $cmd){
      $cmd = "lcd";
      push @cmdParams, "name=" . shift @_;
    }elsif($arg =~ /^(--orient|orient)$/ and @_ > 0 and not defined $cmd){
      $cmd = "orient";
      push @cmdParams, "orient=" . shift @_;
    }elsif($arg =~ /^(--framebuf|framebuf)$/ and @_ > 0 and not defined $cmd){
      $cmd = "framebuf";
      push @cmdParams, "framebuf=" . shift @_;
    }elsif($arg =~ /^(--upload|upload)$/ and @_ > 0 and not defined $cmd){
      $cmd = "upload";
      $cmdFile = shift @_;
      my $destFilename;
      if(@_ > 0){
        $destFilename = shift @_;
      }else{
        $destFilename = $cmdFile;
      }
      push @cmdParams, "filename=$destFilename";
    }elsif($arg =~ /^(--delete|delete)$/ and @_ > 0 and not defined $cmd){
      $cmd = "delete";
      push @cmdParams, "filename=" . shift @_;
    }elsif($arg =~ /^(--bootloader|bootloader|--bootsel|bootsel)$/ and not defined $cmd){
      $cmd = "bootloader";
      $$opts{curlMaxTime} = 3;
    }elsif($arg !~ /^-/ and not defined $cmd and not defined $cmdData){
      $cmdData = $arg;
      $cmd = "text";
      @cmdParams = (@DEFAULT_MARKUP_TEXT_PARAMS, @cmdParams);
      $isBodyPrependSize = 1 if not defined $isBodyPrependSize;
    }else{
      die "ERROR: unknown arg $arg\n";
    }
  }

  if(not defined $ipAddr){
    if(not defined $devName){
      die "ERROR: no devices configured\n" if @DEVICE_NAMES == 0;
      $devName = $DEVICE_NAMES[0];
    }

    $ipAddr = $DEVICE_IPS_BY_NAME{lc $devName};
    if(not defined $ipAddr){
      die "ERROR: could not find IP address for $devName in $DEVICE_CONFIG_FILE\n";
    }
  }

  if($mode eq $MODE_CMD){
    if($cmd eq "text" and $isBodyPrependSize and defined $cmdData){
      my ($windowWidthPx, $windowHeightPx) = fetchWindowGeometry($ipAddr);
      my $size = calculateMaxFontSize($cmdData, $windowWidthPx, $windowHeightPx);
      $cmdData = "[size=$size]$cmdData";
    }
    runCurlCmd($opts, $ipAddr, $cmd, \@cmdParams, $cmdData, $cmdFile);
  }elsif($mode eq $MODE_TEMPLATE_SET){
    my $set = $$TEMPLATE_SETS{$templateSet};
    for my $templateName(sort keys %$set){
      my $markup = $$set{$templateName};
      runCurlCmd($opts, $ipAddr, "template", ["templateName=$templateName"], $markup, undef);
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub fetchWindowGeometry($){
  my ($ipAddr) = @_;
  my $info = `curl --silent --max-time 3 'http://$ipAddr/info'`;
  if($info =~ /window:\s+(\d+)x(\d+)/){
    return ($1, $2);
  }else{
    return ($DEFAULT_LCD_WIDTH, $DEFAULT_LCD_HEIGHT);
  }
}

sub calculateMaxFontSize($$$){
  my ($markup, $windowWidthPx, $windowHeightPx) = @_;
  my $text = $markup;

  #remove '\r' chars
  $text =~ s/\r\n/\n/g;
  $text =~ s/\r/\n/g;

  #replace markup literals and remove other markup (don't count towards char counts)
  $text =~ s/\[\[/_/g;      #[[  => _
  $text =~ s/\[n\]/\n/g;    #[n] => \n
  $text =~ s/\[[^\]]+\]//g; #remove all [markup]

  my @lines = split /\n/, $text;

  my $charsH = @lines;
  my $charsW = 0;
  for my $line(@lines){
    $charsW = length $line if length $line > $charsW;
  }
  $charsW = 1 if $charsW <= 1;
  $charsH = 1 if $charsH <= 1;

  my $vspace = 1;
  my $hspace = 1;

  #select the largest size that will fit all characters,
  #  vertically and horizontally,
  #  or 1 if they cannot fit
  my $hSize = int($windowWidthPx / ($charsW*($FONT_BASE_WIDTH + $hspace)));
  my $vSize = int($windowHeightPx / ($charsH*($FONT_BASE_HEIGHT + $vspace)));
  my $size = $hSize < $vSize ? $hSize : $vSize;
  $size = 1 if $size < 1;

  return $size;
}

sub runCurlCmd($$$$$$){
  my ($opts, $ipAddr, $cmd, $cmdParams, $cmdData, $cmdFile) = @_;
  my $paramsFmt = "";
  if(@$cmdParams > 0){
    my @fmtParams;
    for my $param(@$cmdParams){
      if($param =~ /^(\w+)=(.+)$/){
        my ($key, $val) = ($1, $2);
        $val = uri_escape($val);
        push @fmtParams, "$key=$val";
      }else{
        die "ERROR: malformed query param KEY=VAL string '$param'\n";
      }
    }
    $paramsFmt = "?" . join "&", @fmtParams;
  }

  my @curlOpts = ("--no-progress-meter");
  if(defined $$opts{curlMaxTime}){
    @curlOpts = (@curlOpts, "--max-time", $$opts{curlMaxTime});
  }

  my @curlCmd = ("curl", @curlOpts, "http://$ipAddr/$cmd$paramsFmt");
  @curlCmd = (@curlCmd, "--data", $cmdData) if defined $cmdData;
  @curlCmd = (@curlCmd, "--upload-file", $cmdFile) if defined $cmdFile;

  print "@curlCmd\n" unless $$opts{quiet};
  system @curlCmd;
}

sub parseConfig($){
  my @devices;
  my @lines = `cat $DEVICE_CONFIG_FILE 2>/dev/null`;
  my $sep = '(?:\s*[=,]\s*)';
  for my $line(@lines){
    if($line =~ /^\s*([0-9a-zA-Z_\-]+)$sep(\d+\.\d+\.\d+\.\d+)(?:$sep(.*))?$/){
      my ($devName, $devIP, $synStr) = ($1, $2, $3);
      $synStr = "" if not defined $synStr;
      my @syns = split /\s+/, $synStr;
      @syns = grep {$_ =~ /^\w+$/} @syns;
      push @devices, {devName => $devName, devIP => $devIP, syns => [@syns]};
    }
  }
  return @devices;
}

&main(@ARGV);
