#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub parseConfig($);

my $EXEC = basename $0;

my $DEVICE_CONFIG_FILE = "$ENV{HOME}/.config/pico-lcd.conf";
my @DEVICES = parseConfig($DEVICE_CONFIG_FILE);
my @DEVICE_NAMES = map {$$_{devName}} @DEVICES;
my %DEVICE_IPS_BY_NAME = map {$$_{devName} => $$_{devIP}} @DEVICES;
my $DEVICE_NAME_REGEX = join "|", @DEVICE_NAMES;

my $SPLIT_LINES_BIG_CHAR_LIMIT = 3000;

my $SPLIT_LINES_BIG_ONLY = "big-only";
my $SPLIT_LINES_ALWAYS = "always";
my $SPLIT_LINES_NEVER = "never";

my $DEFAULT_LCD_WIDTH = 240;
my $DEFAULT_LCD_HEIGHT = 240;

my $FONT_BASE_HEIGHT = 8;
my $FONT_BASE_WIDTH = 5;

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS] text   TEXT
  $EXEC [OPTS] --text TEXT
  $EXEC [OPTS] --text=TEXT
    -parse IP_ADDRESS from $DEVICE_CONFIG_FILE
    -modify TEXT with markup
      -see: --color/--size/--hspace/--vspace/--x/--y/--lcd-width/--lcd-height
    -possibly split text into lines, add x/y markup, and run curl for each line
      -see: --lines/--no-lines/--lines-big-only
    -set CLEAR to 'true' or 'false' based on --clear-text/--no-clear-text
    -otherwise:
      -run: curl 'http://<IP_ADDRESS>/text?clear=<CLEAR>&show=true' --data '<TEXT>'

  $EXEC [OPTS] WORD_OR_FILE [WORD_OR_FILE WORD_OR_FILE]
    -NOTE: the first WORD_OR_FILE cannot start with '-'
    -combine WORD_OR_FILE args into TEXT
      -if WORD_OR_FILE is a file:
        -append contents of WORD_OR_FILE to TEXT
      -otherwise:
        -replace '\\n' with literal newline in WORD_OR_FILE
        -if TEXT is not empty, append a space character
        -append ARG to TEXT
    -same as: $EXEC --text=TEXT

  $EXEC [OPTS] cmd   CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
  $EXEC [OPTS] --cmd CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
  $EXEC [OPTS] --cmd=CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
    -parse IP_ADDRESS from $DEVICE_CONFIG_FILE
    -run: curl 'http://<IP_ADDRESS>/<CMD>?<PARAM_NAME>=<PARAM_VAL>&<PARAM_NAME>=<PARAM_VAL>'
        PARAM_NAME=PARAM_VAL
          pass in as URL param in curl
          PARAM_NAME and can contain only letters, numbers, and underscores

  $EXEC [OPTS] info | --info
    fetch+show current state
      -same as: $EXEC [OPTS] --cmd info

  $EXEC [OPTS] connect | --connect
    re-connect to wifi or setup access point
      -same as: $EXEC [OPTS] --cmd connect

  $EXEC [OPTS] ssid | --ssid SSID PASSWORD
    append a network 'SSID = PASSWORD' for next boot/connect
      -same as: $EXEC [OPTS] --ssid ssid=SSID password=PASSWORD

  $EXEC [OPTS] resetwifi | --resetwifi
    REMOVE all configured wifi network SSIDs for next boot/connect
      -same as: $EXEC [OPTS] --cmd resetwifi

  $EXEC [OPTS] clear | --clear
    clear the entire LCD memory
      -same as: $EXEC [OPTS] --cmd clear

  $EXEC [OPTS] show | --show
    show the framebuffer, if any
      -same as: $EXEC [OPTS] --cmd show

  $EXEC [OPTS] --set-timeout [TIMEOUT_MILLIS TIMEOUT_TEXT]
    write \"TIMEOUT_MILLIS,TIMEOUT_TEXT\" to timeout.txt file
      after TIMEOUT_MILLIS milliseconds of no network commands,
      clear LCD, set LCD text to TIMEOUT_TEXT, and show LCD
    if TIMEOUT_MILLIS or TIMEOUT_TEXT is omitted, the timeout is removed,
      and the socket blocks forever

  $EXEC [OPTS] buttons | --buttons
    fetch button press counts and print them
      -same as: $EXEC [OPTS] --cmd buttons

  $EXEC [OPTS] fill   COLOR
  $EXEC [OPTS] --fill COLOR
  $EXEC [OPTS] --fill=COLOR
    fill the window with COLOR
      -same as: $EXEC [OPTS] --cmd fill color=<COLOR>
        COLOR
          one of: white black red green blue

  $EXEC [OPTS] lcd   NAME
  $EXEC [OPTS] --lcd NAME
  $EXEC [OPTS] --lcd=NAME
    set the lcd model to NAME
      -same as: $EXEC [OPTS] --cmd lcd name=<LCD_NAME>
        LCD_NAME
          one of: 1_3 2_0

  $EXEC [OPTS] orient   ORIENT
  $EXEC [OPTS] --orient ORIENT
  $EXEC [OPTS] --orient=ORIENT
    set the rotation
      -same as: $EXEC [OPTS] --cmd orient orient=<ORIENT>
        ORIENT
          landscape | 0 | normal | default
            set rotation to 0 degrees
          portrait | 270 | -90
            set rotation to 270 degrees
          inverted-landscape | 180
            set rotation to 180 degrees
          inverted-portrait | 90
            set rotation to 90 degrees

  $EXEC [OPTS] framebuf   FRAMEBUF
  $EXEC [OPTS] --framebuf FRAMEBUF
  $EXEC [OPTS] --framebuf=FRAMEBUF
    -same as: $EXEC [OPTS] --cmd framebuf enabled=ENABLED \\
                maxwidth=MAX_WIDTH maxheight=MAX_HEIGHT x=OFFSET_X y=OFFSET_Y
      FRAMEBUF
        one of:
          on | enabled | true
            enable framebuf, with no max WxH or X/Y offsets
          off | disabled | false
            disable framebuf
          WxH
            enable framebuf, and set max WxH with no X/Y offsets
          WxH+X+Y
            enable framebuf, and set max WxH and X/Y offsets
      W
        limit the framebuf max width, 0 means no maximum
      H
        limit the framebuf max height, 0 means no maximum
      X
        shift the framebuf window to the right, 0 means no offset
      Y
        shift the framebuf window down, 0 means no offset

      NOTE: regardless of current orientation:
        W and X always refers to the larger physical dimension of the LCD
        H and Y always refers to the smaller physical dimension of the LCD


  $EXEC [OPTS] left | --left
    -same as: $EXEC [OPTS] framebuf 160x240+0+0

  $EXEC [OPTS] right | --right
    -same as: $EXEC [OPTS] framebuf 160x240+160+0

  $EXEC [OPTS] top | --top
    -same as: $EXEC [OPTS] framebuf 320x120+0+0

  $EXEC [OPTS] bot | --bot | bottom | --bottom
    -same as: $EXEC [OPTS] framebuf 320x120+0+120


  $EXEC [OPTS] square | --square
    -same as: $EXEC [OPTS] framebuf 240x240

  OPTS
    -q | --quiet
      do not print messages sent with curl

    -m TIMEOUT_FRAC_SECONDS
    --max-time TIMEOUT_FRAC_SECONDS
    --max-time=TIMEOUT_FRAC_SECONDS
    --timeout TIMEOUT_FRAC_SECONDS
    --timeout=TIMEOUT_FRAC_SECONDS
      pass '--max-time TIMEOUT_FRAC_SECONDS' to all calls to curl
    --data=DATA
      for --cmd, pass in DATA as POST payload with curl (using '--data DATA')

    --ip=IP_ADDRESS
      use IP_ADDRESS in calls to curl
    --ap
      use IP_ADDRESS=192.168.4.1
    DEV_NAME | --dev=DEV_NAME | --dev DEV_NAME
    DEV_NAME | --dev=DEV_NAME | --dev DEV_NAME
      -parse contents of $DEVICE_CONFIG_FILE
      -find line that matches this device: DEV_NAME = IP_ADDRESS
      -same as: --ip=IP_ADDRESS
        DEV_NAME
          any of: @DEVICE_NAMES
    --default-dev | --any-dev
      -parse contents of $DEVICE_CONFIG_FILE
      -find first line that matches any device: DEV_NAME = IP_ADDRESS
      -same as: --ip=IP_ADDRESS
      (this is the default)

    --framebuf=FRAMEBUF
    --orient=ORIENT
    --left
    --top
    --right
    --bot
    --bottom
    --square
      if specified after TEXT, send orient/framebuf params as part of the /text cmd

    --clear-text
      when setting text, clear the screen first
        if multiple messages (e.g.: with --lines):
          send param clear=true for first message, clear=false for others
        otherwise:
          send param clear=true with text
       (this is the default)
    --no-clear-text | --no-clear
        always send clear=false with all text commands

    --color=COLOR
      prefix all TEXT with '!color=COLOR!'
    --size=SIZE
      prefix all TEXT with '!size=SIZE!'
        (this is the number of pixels per dot, in a 5-dot by 8-dot font)
      NOTE: the default is to calculate the largest SIZE that fits all characters
    --hspace=HSPACE
      prefix all TEXT with '!hspace=HSPACE!'
    --vspace=VSPACE
      prefix all TEXT with '!vspace=VSPACE!'
    --x=X
      prefix all TEXT with '!x=X!'
    --y=Y
      prefix all TEXT with '!y=Y!'

    --lcd-width=WIDTH_PX
      select a SIZE based on WIDTH_PX, HSPACE, and number of chars in longest line of TEXT
    --lcd-height=HEIGHT_PX
      select a SIZE based on HEIGHT_PX, VSPACE, and number of lines in TEXT

    --lines
      -split TEXT into lines, TEXT_LINE
      -for each TEXT_LINE:
        -calculate the OFFSET for each line
          -first line is OFFSET=0
          -add SIZE*8 for each subsequent line
          -assume SIZE=5 if not given
        -prefix each TEXT_LINE with '!y=OFFSET!'
        -treat TEXT_LINE as TEXT, and prefix with --color/--size/etc
        -calculate SHOW as 'true' for the last TEXT_LINE, and 'false' otherwise
        -calculate CLEAR as 'true' for the first TEXT_LINE, and 'false' otherwise
          -unless --no-clear-text is given, then false always
        -run: curl 'http://<IP_ADDRESS>/text?clear=CLEAR&show=SHOW' 'TEXT_LINE'
    --no-lines
      never split lines as in --lines
    --lines-big-only
      if TEXT is $SPLIT_LINES_BIG_CHAR_LIMIT chars or more:
        same as: --lines
      otherwise:
        same as: --no-lines
      (this is the default)
";

my $MODE_TEXT = "text";
my $MODE_CMD = "cmd";

sub main(@){
  my $mode = undef;
  my $ipAddr = undef;
  my $devName = undef;
  my $quiet = 0;
  my $curlMaxTime = undef;

  my $text = "";
  my @textParams;
  my $isClearText = 1;
  my $splitLines = $SPLIT_LINES_BIG_ONLY;
  my $lcdWidthPx = undef;
  my $lcdHeightPx = undef;
  my ($color, $size, $hspace, $vspace, $x, $y);

  my $cmd = undef;
  my @cmdParams;
  my $cmdData = undef;

  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-q|--quiet)$/){
      $quiet = 1;
    }elsif($arg =~ /^(-m|--max-time|--timeout)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $curlMaxTime = shift @_;
    }elsif($arg =~ /^(?:--max-time|--timeout)=(\d+|\d*\.\d+)$/){
      $curlMaxTime = $1;
    }elsif($arg =~ /^--ip=(.+)$/){
      $ipAddr = $1
    }elsif($arg =~ /^(--ap)$/){
      $ipAddr = '192.168.4.1';
    }elsif($arg =~ /^--dev=(\w+)$/){
      $devName = $1;
    }elsif($arg =~ /^--dev$/ and @_ > 0 and $_[0] =~ /^(\w+)$/){
      $devName = shift @_;
    }elsif($arg =~ /^($DEVICE_NAME_REGEX)$/){
      $devName = $1;
    }elsif($arg =~ /^(--default-dev|--any-dev)$/){
      $devName = undef;
    }elsif($arg =~ /^(--text|text)$/ and @_ > 0 and not defined $mode){
      $text = shift @_;
      $mode = $MODE_TEXT;
    }elsif($arg =~ /^--text=(.+)$/ and not defined $mode){
      $text = $1;
      $mode = $MODE_TEXT;
    }elsif($arg =~ /^(--cmd|cmd)$/ and @_ > 0 and not defined $mode){
      $cmd = shift @_;
      $mode = $MODE_CMD;
      while(@_ > 0 and $_[0] =~ /^\w+=.+$/){
        push @cmdParams, shift @_;
      }
    }elsif($arg =~ /^--cmd=(\w+)$/ and not defined $mode){
      $cmd = $1;
      $mode = $MODE_CMD;
      while(@_ > 0 and $_[0] =~ /^\w+=\w+$/){
        push @cmdParams, shift @_;
      }
    }elsif($arg =~ /^--data=(.+)$/){
      $cmdData = $1;
      die "ERROR: --data must follow --cmd\n" if $mode ne $MODE_CMD;
    }elsif($arg =~ /^(--info|info)$/ and not defined $mode){
      $cmd = "info";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--connect|connect)$/ and not defined $mode){
      $cmd = "connect";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--ssid|ssid)$/ and @_ >= 2 and not defined $mode){
      $cmd = "ssid";
      $mode = $MODE_CMD;
      push @cmdParams, "ssid=" . shift @_;
      push @cmdParams, "password=" . shift @_;
    }elsif($arg =~ /^(--resetwifi|resetwifi)$/ and not defined $mode){
      $cmd = "resetwifi";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--clear|clear)$/ and not defined $mode){
      $cmd = "clear";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--show|show)$/ and not defined $mode){
      $cmd = "show";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--buttons|buttons)$/ and not defined $mode){
      $cmd = "buttons";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--set-timeout)$/ and not defined $mode){
      $cmd = "timeout";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--fill|fill)$/ and @_ > 0 and not defined $mode){
      $cmd = "fill";
      $mode = $MODE_CMD;
      push @cmdParams, "color=" . shift @_;
    }elsif($arg =~ /^--fill=(\w+)$/ and not defined $mode){
      $cmd = "fill";
      $mode = $MODE_CMD;
      push @cmdParams, "color=$1";
    }elsif($arg =~ /^(--lcd|lcd)$/ and @_ > 0 and not defined $mode){
      $cmd = "lcd";
      $mode = $MODE_CMD;
      push @cmdParams, "name=" . shift @_;
    }elsif($arg =~ /^--lcd=(\w+)$/ and not defined $mode){
      $cmd = "lcd";
      $mode = $MODE_CMD;
      push @cmdParams, "name=$1";
    }elsif($arg =~ /^(--orient|orient)$/ and @_ > 0 and not defined $mode){
      $cmd = "orient";
      $mode = $MODE_CMD;
      push @cmdParams, "orient=" . shift @_;
    }elsif($arg =~ /^--orient=(\w+)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "orient";
        $mode = $MODE_CMD;
        push @cmdParams, "orient=$1";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "orient=$1";
      }
    }elsif($arg =~ /^(--framebuf|framebuf)$/ and @_ > 0 and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=" . shift @_;
    }elsif($arg =~ /^--framebuf=(.+)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=$1";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "framebuf=$1";
      }
    }elsif($arg =~ /^(left)$/ and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=160x240+0+0";
    }elsif($arg =~ /^(--left)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=160x240+0+0";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "framebuf=160x240+0+0";
      }
    }elsif($arg =~ /^(top)$/ and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=320x120+0+0";
    }elsif($arg =~ /^(--top)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=320x120+0+0";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "framebuf=320x120+0+0";
      }
    }elsif($arg =~ /^(right)$/ and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=160x240+160+0";
    }elsif($arg =~ /^(--right)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=160x240+160+0";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "framebuf=160x240+160+0";
      }
    }elsif($arg =~ /^(bot|bottom)$/ and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=320x120+0+120";
    }elsif($arg =~ /^(--bot|--bottom)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=320x120+0+120";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "framebuf=320x120+0+120";
      }
    }elsif($arg =~ /^(square)$/ and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=240x240";
    }elsif($arg =~ /^(--square)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=240x240";
      }else{
        push @textParams, "framebuf=240x240";
      }
    }elsif($arg =~ /^(--clear-text)$/){
      $isClearText = 1;
    }elsif($arg =~ /^(--no-clear-text|--no-clear)$/){
      $isClearText = 0;
    }elsif($arg =~ /^--lcd-width=(\d+)$/){
      $lcdWidthPx = $1;
    }elsif($arg =~ /^--lcd-height=(\d+)$/){
      $lcdHeightPx = $1;
    }elsif($arg =~ /^(--lines)$/){
      $splitLines = $SPLIT_LINES_ALWAYS;
    }elsif($arg =~ /^(--no-lines)$/){
      $splitLines = $SPLIT_LINES_NEVER;
    }elsif($arg =~ /^(--lines-big-only)$/){
      $splitLines = $SPLIT_LINES_BIG_ONLY;
    }elsif($arg =~ /^--color=(.+)$/){
      $color = $1;
    }elsif($arg =~ /^--size=(.+)$/){
      $size = $1;
    }elsif($arg =~ /^--hspace=(.+)$/){
      $hspace = $1;
    }elsif($arg =~ /^--vspace=(.+)$/){
      $vspace = $1;
    }elsif($arg =~ /^--x=(.+)$/){
      $x = $1;
    }elsif($arg =~ /^--y=(.+)$/){
      $y = $1;
    }elsif(-f $arg and (not defined $mode or $mode eq $MODE_TEXT)){
      open FH, "< $arg" or die "ERROR: could not read $arg\n$!\n";
      my $contents = join '', <FH>;
      close FH;
      $text .= $contents;
      $mode = $MODE_TEXT;
    }elsif((defined $mode and $mode eq $MODE_TEXT) or ($arg !~ /^-/ and not defined $mode)){
      $text .= " " if $text ne "";
      $text .= $arg;
      $mode = $MODE_TEXT;
    }elsif((defined $cmd and $cmd eq "timeout") and @cmdParams == 0){
      push @cmdParams, "timeoutMillis=$arg";
    }elsif((defined $cmd and $cmd eq "timeout") and @cmdParams == 1 and not defined $cmdData){
      $cmdData = $arg;
    }else{
      die "ERROR: unknown arg $arg\n";
    }
  }

  if(not defined $ipAddr){
    for my $cfgDevName(@DEVICE_NAMES){
      if(not defined $devName or (lc $devName eq lc $cfgDevName)){
        $ipAddr = $DEVICE_IPS_BY_NAME{$cfgDevName};
        last;
      }
    }
    if(not defined $ipAddr){
      my $dev = defined $devName ? $devName : "any dev";
      die "ERROR: could not find IP address for $dev in $DEVICE_CONFIG_FILE\n";
    }
  }

  $mode = $MODE_TEXT if not defined $mode;

  my @curlOpts = ("--no-progress-meter");
  @curlOpts = (@curlOpts, "--max-time", $curlMaxTime) if defined $curlMaxTime;

  if($mode eq $MODE_CMD){
    my $paramsFmt = "";
    if(@cmdParams > 0){
      $paramsFmt = "?" . join "&", @cmdParams;
    }

    my @curlCmd = ("curl", @curlOpts, "http://$ipAddr/$cmd$paramsFmt");
    @curlCmd = (@curlCmd, "--data", $cmdData) if defined $cmdData;
    system @curlCmd;
  }elsif($mode eq $MODE_TEXT){
    my $charsTotal = length $text;

    my $isLines;
    if($splitLines eq $SPLIT_LINES_ALWAYS){
      $isLines = 1;
    }elsif($splitLines eq $SPLIT_LINES_BIG_ONLY){
      $isLines = $charsTotal > $SPLIT_LINES_BIG_CHAR_LIMIT ? 1 : 0;
    }else{
      $isLines = 0;
    }

    #remove '\r' chars
    $text =~ s/\r\n/\n/g;
    $text =~ s/\r/\n/g;

    #replace '!n!' markup with newlines
    my $newText;
    for(my $i=0; $i<length $text; $i++){
      my $ch = substr($text, $i, 1);
      if($ch eq "!"){
        my $cmd = "!";
        $i++;
        while($i < length $text and substr($text, $i, 1) ne "!"){
          $cmd .= substr($text, $i, 1);
          $i++;
        }
        if($i < length $text){
          $cmd .= substr($text, $i, 1);
          die "ERROR: failed to parse $text\n" if $cmd !~ /^![^!]*!$/;
        }else{
          die "ERROR: failed to parse $text\n" if $cmd !~ /^![^!]*$/;
        }
        if($cmd eq "!n!"){
          $newText .= "\n";
        }else{
          $newText .= $cmd;
        }
      }else{
        $newText .= $ch;
      }
    }
    $text = $newText;

    my @lines = split /\n/, $text;

    my $charsH = @lines;
    my $charsW = 0;
    for my $line(@lines){
      $charsW = length $line if length $line > $charsW;
    }
    $charsW = 1 if $charsW <= 1;
    $charsH = 1 if $charsH <= 1;

    $vspace = 1 if not defined $vspace;
    $hspace = 1 if not defined $hspace;

    #select the largest size that will fit all characters,
    #  vertically and horizontally,
    #  or 1 if they cannot fit
    if(not defined $size){
      if(not defined $lcdWidthPx or not defined $lcdHeightPx){
        my $info = `curl @curlOpts 'http://$ipAddr/info'`;
        if($info =~ /window:\s+(\d+)x(\d+)/){
          $lcdWidthPx = $1;
          $lcdHeightPx = $2;
        }else{
          $lcdWidthPx = $DEFAULT_LCD_WIDTH;
          $lcdHeightPx = $DEFAULT_LCD_HEIGHT;
        }
      }

      my $hSize = int($lcdWidthPx / ($charsW*($FONT_BASE_WIDTH + $hspace)));
      my $vSize = int($lcdHeightPx / ($charsH*($FONT_BASE_HEIGHT + $vspace)));
      $size = $hSize < $vSize ? $hSize : $vSize;
      $size = 1 if $size < 1;
    }

    my @messages;
    if($isLines){
      my $height = ($FONT_BASE_HEIGHT+$vspace)*$size;
      my $offset = 0;
      for my $line(@lines){
        push @messages, "!x=0!!y=$offset!$line";
        $offset += $height;
      }
    }else{
      if(length $text > 0){
        @messages = ($text);
      }
    }

    @messages = grep {length $_ > 0} @messages;

    for(my $i=0; $i<@messages; $i++){
      my $msg = $messages[$i];
      $msg = "!size=$size!$msg" if defined $size;
      $msg = "!color=$color!$msg" if defined $color;
      $msg = "!hspace=$hspace!$msg" if defined $hspace and $hspace != 1.0;
      $msg = "!vspace=$vspace!$msg" if defined $vspace and $vspace != 1.0;
      $msg = "!x=$x!$msg" if defined $x;
      $msg = "!y=$y!$msg" if defined $y;

      print "$msg\n" unless $quiet;

      my @params = (@textParams);
      #clear() on the first message unless --no-clear
      if($isClearText and $i == 0){
        @params = (@params, "clear=true");
      }else{
        @params = (@params, "clear=false");
      }

      #do not show() until last message
      if($i < $#messages){
        @params = (@params, "show=false");
      }else{
        @params = (@params, "show=true");
      }

      my $paramsFmt = "?" . join "&", @params;
      system "curl", @curlOpts,
        "http://$ipAddr/text$paramsFmt",
        "--data", "$msg";
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub parseConfig($){
  my @devices;
  my @lines = `cat $DEVICE_CONFIG_FILE 2>/dev/null`;
  for my $line(@lines){
    if($line =~ /^\s*(\w+)\s*=\s*(\d+\.\d+\.\d+\.\d+)$/){
      my ($devName, $devIP) = ($1, $2);
      push @devices, {devName => $devName, devIP => $devIP};
    }
  }
  return @devices;
}

&main(@ARGV);
