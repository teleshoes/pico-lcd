#!/usr/bin/perl
use strict;
use warnings;
use File::Basename qw(basename);

sub runCurlCmd($$$$$$);
sub parseConfig($);

my $EXEC = basename $0;

my $DEVICE_CONFIG_FILE = "$ENV{HOME}/.config/pico-lcd.conf";
my @DEVICES = parseConfig($DEVICE_CONFIG_FILE);
my @DEVICE_NAMES = map {$$_{devName}} @DEVICES;
my @DEVICE_SYNS = (map {@{$$_{syns}}} @DEVICES);
my %DEVICE_IPS_BY_NAME = map {
  my $ip = $$_{devIP}; map {$_ => $ip} ($$_{devName}, @{$$_{syns}})
} @DEVICES;
my $DEVICE_NAME_REGEX = join "|", (@DEVICE_NAMES, @DEVICE_SYNS);

my $SPLIT_LINES_BIG_CHAR_LIMIT = 3000;

my $SPLIT_LINES_BIG_ONLY = "big-only";
my $SPLIT_LINES_ALWAYS = "always";
my $SPLIT_LINES_NEVER = "never";

my $DEFAULT_LCD_WIDTH = 240;
my $DEFAULT_LCD_HEIGHT = 240;

my $FONT_BASE_HEIGHT = 8;
my $FONT_BASE_WIDTH = 5;

my $CLOCK_MARKUP = ""
  . "!size=4!!hr!!n!"
  . "!color=green!!size=8!!rtc=%H:%M!!n!"
  . "!color=white!!hr!!size=4!!n!"
  . " !rtc=%H:%M:%S!"
;

my $TEMPLATE_SETS = {
  "default" => {
    "timeout"        => "",
    "wifi-waiting"   => "",
    "wifi-connected" => "",
    "ap-waiting"     => "",
    "ap-active"      => "",
  },
  "clock" => {
    "timeout" => ""
      . "!size=4!!rtc=%Y-%m-%d!!n!"
      . $CLOCK_MARKUP,
    "wifi-waiting" => ""
      . "!size=2!WAITING WIFI   !rtc=%Y-%m-%d!!n!"
      . "!var=ssid!!n!"
      . $CLOCK_MARKUP,
    "wifi-connected" => ""
      . "!size=2!CONNECTED      !rtc=%Y-%m-%d!!n!"
      . "!var=ip!!n!"
      . $CLOCK_MARKUP,
    "ap-waiting" => ""
      . "!size=2!AP WAITING     !rtc=%Y-%m-%d!!n!"
      . "!var=ssid!!n!"
      . $CLOCK_MARKUP,
    "ap-active" => ""
      . "!size=2!ssid=!var=ssid!  !rtc=%Y-%m-%d!!n!"
      . "pw=!var=password!   !var=ip!!n!"
      . $CLOCK_MARKUP,
  },
};

my $USAGE = "Usage:
  $EXEC -h | --help
    show this message

  $EXEC [OPTS] text   TEXT
  $EXEC [OPTS] --text TEXT
  $EXEC [OPTS] --text=TEXT
    -parse IP_ADDRESS from $DEVICE_CONFIG_FILE
    -modify TEXT with markup
      -see: --color/--size/--hspace/--vspace/--x/--y/--lcd-width/--lcd-height
    -possibly split text into lines, add x/y markup, and run curl for each line
      -see: --lines/--no-lines/--lines-big-only
    -set CLEAR to 'true' or 'false' based on --clear-text/--no-clear-text
    -otherwise:
      -run: curl 'http://<IP_ADDRESS>/text?clear=<CLEAR>&show=true' --data '<TEXT>'

  $EXEC [OPTS] WORD_OR_FILE [WORD_OR_FILE WORD_OR_FILE]
    -NOTE: the first WORD_OR_FILE cannot start with '-'
    -combine WORD_OR_FILE args into TEXT
      -if WORD_OR_FILE is a file:
        -append contents of WORD_OR_FILE to TEXT
      -otherwise:
        -replace '\\n' with literal newline in WORD_OR_FILE
        -if TEXT is not empty, append a space character
        -append ARG to TEXT
    -same as: $EXEC --text=TEXT

  $EXEC [OPTS] --template-set TEMPLATE_SET_NAME
    apply pre-configured set of markup templates
    TEMPLATE_SET_NAME: " . join(" | ", sort keys %$TEMPLATE_SETS) . "

  $EXEC [OPTS] cmd   CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
  $EXEC [OPTS] --cmd CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
  $EXEC [OPTS] --cmd=CMD [PARAM_NAME=PARAM_VAL PARAM_NAME=PARAM_VAL ..]
    -parse IP_ADDRESS from $DEVICE_CONFIG_FILE
    -run: curl 'http://<IP_ADDRESS>/<CMD>?<PARAM_NAME>=<PARAM_VAL>&<PARAM_NAME>=<PARAM_VAL>'
        PARAM_NAME=PARAM_VAL
          pass in as URL param in curl
          PARAM_NAME and can contain only letters, numbers, and underscores

  $EXEC [OPTS] info | --info
    fetch+show current state
      -same as: $EXEC [OPTS] --cmd info

  $EXEC [OPTS] connect | --connect
    re-connect to wifi or setup access point
      -same as: $EXEC [OPTS] --cmd connect

  $EXEC [OPTS] ssid | --ssid SSID PASSWORD [TIMEOUT]
    append a network for next boot/connect
      formatted:
        'TIMEOUT,SSID = PASSWORD'
        or
        'SSID = PASSWORD'
    -same as: $EXEC [OPTS] --ssid ssid=SSID password=PASSWORD timeout=TIMEOUT

  $EXEC [OPTS] resetwifi | --resetwifi
    REMOVE all configured wifi network SSIDs for next boot/connect
      -same as: $EXEC [OPTS] --cmd resetwifi

  $EXEC [OPTS] template TEMPLATE_NAME [TEMPLATE_MARKUP]
    write \"TEMPLATE_MARKUP\" to state-template-TEMPLATE_NAME file
      TEMPLATE_NAME: timeout | wifi-waiting | wifi-connected | ap-waiting | ap-active
    if TEMPLATE_MARKUP is omitted, the default markup for that template is used

  $EXEC [OPTS] timeout [TIMEOUT_MILLIS]
  $EXEC [OPTS] --timeout [TIMEOUT_MILLIS]
    write \"TIMEOUT_MILLIS\" to state-timeout file
      after TIMEOUT_MILLIS milliseconds of no network commands,
      clear LCD, set LCD text to 'timeout' template, and show LCD
    if TIMEOUT_MILLIS is omitted, the timeout is removed,
      and the socket blocks forever

  $EXEC [OPTS] timezone [TZDATA_ZONE_NAME]
    write \"TZDATA_ZONENAME\" to state-timezone file
      used for DS3231 real-time-clock in !rtc! markup
      requires calculating and installing tzdata CSV in advance
    if TZDATA_ZONE_NAME is omitted, delete state-timezone file

  $EXEC [OPTS] rtc RTC_EPOCH
    set the DS3231 RTC epoch to RTC_EPOCH

  $EXEC [OPTS] clear | --clear
    clear the entire LCD memory
      -same as: $EXEC [OPTS] --cmd clear

  $EXEC [OPTS] show | --show
    show the framebuffer, if any
      -same as: $EXEC [OPTS] --cmd show

  $EXEC [OPTS] buttons | --buttons
    fetch button press counts and print them
      -same as: $EXEC [OPTS] --cmd buttons

  $EXEC [OPTS] fill   COLOR
  $EXEC [OPTS] --fill COLOR
  $EXEC [OPTS] --fill=COLOR
    fill the window with COLOR
      -same as: $EXEC [OPTS] --cmd fill color=<COLOR>
        COLOR
          one of: white black red green blue

  $EXEC [OPTS] lcd   NAME
  $EXEC [OPTS] --lcd NAME
  $EXEC [OPTS] --lcd=NAME
    set the lcd model to NAME
      -same as: $EXEC [OPTS] --cmd lcd name=<LCD_NAME>
        LCD_NAME
          one of: 1_3 2_0

  $EXEC [OPTS] orient   ORIENT
  $EXEC [OPTS] --orient ORIENT
  $EXEC [OPTS] --orient=ORIENT
    set the rotation
      -same as: $EXEC [OPTS] --cmd orient orient=<ORIENT>
        ORIENT
          landscape | 0 | normal | default
            set rotation to 0 degrees
          portrait | 270 | -90
            set rotation to 270 degrees
          inverted-landscape | 180
            set rotation to 180 degrees
          inverted-portrait | 90
            set rotation to 90 degrees

  $EXEC [OPTS] framebuf   FRAMEBUF
  $EXEC [OPTS] --framebuf FRAMEBUF
  $EXEC [OPTS] --framebuf=FRAMEBUF
    -same as: $EXEC [OPTS] --cmd framebuf framebuf=FRAMEBUF
        FRAMEBUF:
          <FB_W>x<FB_H>+<FB_X>+<FB_Y> = enable framebuf with WxH and offset (0, 0)
          <FB_W>x<FB_H> = same as <FB_W>x<FB_H>+0+0
          off           = disable the framebuf
          <FB_NAME>     = one of: full | left | right | top | bottom | square
          full          = same as <LCD_W>x<LCD_H>                      e.g.: 320x240
          left          = same as <HALF_LCD_W>x<LCD_H>                 e.g.: 160x240
          right         = same as <HALF_LCD_W>x<LCD_H>+<HALF_LCD_W>+0  e.g.: 160x240+160+0
          top           = same as <LCD_W>x<HALF_LCD_H>                 e.g.: 320x120
          bottom        = same as <LCD_W>x<HALF_LCD_H>+0+<HALF_LCD_H>  e.g.: 320x120+0+120
          square        = same as <LCD_H>x<LCD_H>                      e.g.: 240x240

        <LCD_W>:      320 for lcd=2_0 or 240 for lcd=1_3
        <LCD_H>:      240 for lcd=2_0 or 240 for lcd=1_3
        <HALF_LCD_W>: 160 for lcd=2_0 or 120 for lcd_1_3
        <HALF_LCD_H>: 120 for lcd_2_0 or 120 for lcd_1_3

        NOTE: 'framebuf' param is *unaffected* by orientation
              <FB_W> and <FB_X> always refer to the longest dimension of the physical LCD
              <FB_H> and <FB_Y> always refer to the shortest dimension of the physical LCD
        NOTE: 'framebuf' uses a lot of memory. if memory allocation fails, framebuf is disabled
              the framebuf, if any, that is actually successfully allocated is returned
              if 'framebuf: off' is returned instead of a framebuf, allocation likely failed

  $EXEC [OPTS] upload   FILE [DEST_FILENAME]
  $EXEC [OPTS] --upload FILE [DEST_FILENAME]
  $EXEC [OPTS] --upload=FILE [DEST_FILENAME]
    upload file FILE to DEST_FILENAME (DEST_FILENAME defaults to FILE if omitted)
    -same as:
      $EXEC [OPTS] --cmd upload filename=DEST_FILENAME --upload-file=FILE

  $EXEC [OPTS] delete   FILENAME
  $EXEC [OPTS] --delete FILENAME
  $EXEC [OPTS] --delete=FILENAME
    delete remote file FILENAME
    -same as:
      $EXEC [OPTS] --cmd delete filename=FILENAME

  $EXEC [OPTS] bootloader | --bootloader | bootsel | --bootsel
    enter bootloader (bootsel mass storage mode)
    also sets curl --max-time to 3s, as it will hang forever otherwise
      -same as: $EXEC [OPTS] --cmd bootloader -m 3


  $EXEC [OPTS] full | --full
    -same as: $EXEC [OPTS] framebuf full
  $EXEC [OPTS] left | --left
    -same as: $EXEC [OPTS] framebuf left
  $EXEC [OPTS] right | --right
    -same as: $EXEC [OPTS] framebuf right
  $EXEC [OPTS] top | --top
    -same as: $EXEC [OPTS] framebuf top
  $EXEC [OPTS] bottom | --bottom
    -same as: $EXEC [OPTS] framebuf bottom
  $EXEC [OPTS] square | --square
    -same as: $EXEC [OPTS] framebuf square

  OPTS
    -q | --quiet
      do not print messages sent with curl

    -m CURL_MAX_TIME_FRAC_SECONDS
    --max-time CURL_MAX_TIME_FRAC_SECONDS
    --max-time=CURL_MAX_TIME_FRAC_SECONDS
      pass '--max-time CURL_MAX_TIME_FRAC_SECONDS' to all calls to curl

    --data=DATA
      for --cmd, pass in DATA as POST payload with curl (using '--data DATA')
    --upload-file=FILENAME
      for --cmd, pass in FILENAME contents as PUT payload with curl
      (using '--upload-file' FILENAME)

    --ip=IP_ADDRESS
      use IP_ADDRESS in calls to curl
    --ap
      use IP_ADDRESS=192.168.4.1
    DEV_NAME | --dev=DEV_NAME | --dev DEV_NAME
    DEV_NAME | --dev=DEV_NAME | --dev DEV_NAME
      -parse contents of $DEVICE_CONFIG_FILE
      -find line that matches this device: DEV_NAME = IP_ADDRESS
      -same as: --ip=IP_ADDRESS
        DEV_NAME
          any of: @DEVICE_NAMES
    --default-dev | --any-dev
      -parse contents of $DEVICE_CONFIG_FILE
      -find first line that matches any device: DEV_NAME = IP_ADDRESS
      -same as: --ip=IP_ADDRESS
      (this is the default)

    --framebuf=FRAMEBUF
    --orient=ORIENT
    --full
    --left
    --top
    --right
    --bottom
    --square
      if specified after TEXT, send orient/framebuf params as part of the /text cmd

    --clear-text
      when setting text, clear the screen first
        if multiple messages (e.g.: with --lines):
          send param clear=true for first message, clear=false for others
        otherwise:
          send param clear=true with text
       (this is the default)
    --no-clear-text | --no-clear
        always send clear=false with all text commands

    --color=COLOR
      prefix all TEXT with '!color=COLOR!'
    --size=SIZE
      prefix all TEXT with '!size=SIZE!'
        (this is the number of pixels per dot, in a 5-dot by 8-dot font)
      NOTE: the default is to calculate the largest SIZE that fits all characters
    --hspace=HSPACE
      prefix all TEXT with '!hspace=HSPACE!'
    --vspace=VSPACE
      prefix all TEXT with '!vspace=VSPACE!'
    --x=X
      prefix all TEXT with '!x=X!'
    --y=Y
      prefix all TEXT with '!y=Y!'

    --lcd-width=WIDTH_PX
      select a SIZE based on WIDTH_PX, HSPACE, and number of chars in longest line of TEXT
    --lcd-height=HEIGHT_PX
      select a SIZE based on HEIGHT_PX, VSPACE, and number of lines in TEXT

    --lines
      -split TEXT into lines, TEXT_LINE
      -for each TEXT_LINE:
        -calculate the OFFSET for each line
          -first line is OFFSET=0
          -add SIZE*8 for each subsequent line
          -assume SIZE=5 if not given
        -prefix each TEXT_LINE with '!y=OFFSET!'
        -treat TEXT_LINE as TEXT, and prefix with --color/--size/etc
        -calculate SHOW as 'true' for the last TEXT_LINE, and 'false' otherwise
        -calculate CLEAR as 'true' for the first TEXT_LINE, and 'false' otherwise
          -unless --no-clear-text is given, then false always
        -run: curl 'http://<IP_ADDRESS>/text?clear=CLEAR&show=SHOW' 'TEXT_LINE'
    --no-lines
      never split lines as in --lines
    --lines-big-only
      if TEXT is $SPLIT_LINES_BIG_CHAR_LIMIT chars or more:
        same as: --lines
      otherwise:
        same as: --no-lines
      (this is the default)
";

my $MODE_TEXT = "text";
my $MODE_CMD = "cmd";
my $MODE_TEMPLATE_SET = "template-set";

sub main(@){
  my $mode = undef;
  my $ipAddr = undef;
  my $devName = undef;
  my $quiet = 0;
  my $curlMaxTime = undef;

  my $text = "";
  my @textParams;
  my $isClearText = 1;
  my $splitLines = $SPLIT_LINES_BIG_ONLY;
  my $lcdWidthPx = undef;
  my $lcdHeightPx = undef;
  my ($color, $size, $hspace, $vspace, $x, $y);

  my $templateSet = undef;

  my $cmd = undef;
  my @cmdParams;
  my $cmdData = undef;
  my $cmdFile = undef;

  while(@_ > 0){
    my $arg = shift @_;
    if($arg =~ /^(-h|--help)$/){
      print $USAGE;
      exit 0;
    }elsif($arg =~ /^(-q|--quiet)$/){
      $quiet = 1;
    }elsif($arg =~ /^(-m|--max-time)$/ and @_ > 0 and $_[0] =~ /^(\d+|\d*\.\d+)$/){
      $curlMaxTime = shift @_;
    }elsif($arg =~ /^(?:--max-time)=(\d+|\d*\.\d+)$/){
      $curlMaxTime = $1;
    }elsif($arg =~ /^--ip=(.+)$/){
      $ipAddr = $1
    }elsif($arg =~ /^(--ap)$/){
      $ipAddr = '192.168.4.1';
    }elsif($arg =~ /^--dev=(\w+)$/){
      $devName = $1;
    }elsif($arg =~ /^--dev$/ and @_ > 0 and $_[0] =~ /^(\w+)$/){
      $devName = shift @_;
    }elsif($arg =~ /^($DEVICE_NAME_REGEX)$/){
      $devName = $1;
    }elsif($arg =~ /^(--default-dev|--any-dev)$/){
      $devName = undef;
    }elsif($arg =~ /^(--text|text)$/ and @_ > 0 and not defined $mode){
      $text = shift @_;
      $mode = $MODE_TEXT;
    }elsif($arg =~ /^--text=(.+)$/ and not defined $mode){
      $text = $1;
      $mode = $MODE_TEXT;
    }elsif($arg =~ /^(--template-set|template-set)$/ and @_ > 0 and not defined $mode){
      $templateSet = shift @_;
      if(not defined $$TEMPLATE_SETS{$templateSet}){
        die "ERROR: unknown template set $templateSet\n";
      }
      $mode = $MODE_TEMPLATE_SET;
    }elsif($arg =~ /^(--cmd|cmd)$/ and @_ > 0 and not defined $mode){
      $cmd = shift @_;
      $mode = $MODE_CMD;
      while(@_ > 0 and $_[0] =~ /^\w+=.+$/){
        push @cmdParams, shift @_;
      }
    }elsif($arg =~ /^--cmd=(\w+)$/ and not defined $mode){
      $cmd = $1;
      $mode = $MODE_CMD;
      while(@_ > 0 and $_[0] =~ /^\w+=\w+$/){
        push @cmdParams, shift @_;
      }
    }elsif($arg =~ /^--data=(.+)$/){
      $cmdData = $1;
      die "ERROR: --data must follow --cmd\n" if $mode ne $MODE_CMD;
    }elsif($arg =~ /^--upload-file=(.+)$/){
      $cmdFile = $1;
      die "ERROR: --upload-file must follow --cmd\n" if $mode ne $MODE_CMD;
    }elsif($arg =~ /^(--info|info)$/ and not defined $mode){
      $cmd = "info";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--connect|connect)$/ and not defined $mode){
      $cmd = "connect";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--ssid|ssid)$/ and @_ >= 2 and not defined $mode){
      $cmd = "ssid";
      $mode = $MODE_CMD;
      push @cmdParams, "ssid=" . shift @_;
      push @cmdParams, "password=" . shift @_;
      if(@_ > 0){
        push @cmdParams, "timeout=" . shift @_;
      }
    }elsif($arg =~ /^(--resetwifi|resetwifi)$/ and not defined $mode){
      $cmd = "resetwifi";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--template|template)$/ and not defined $mode){
      $cmd = "template";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--timeout|timeout)$/ and not defined $mode){
      $cmd = "timeout";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--timezone|timezone)$/ and not defined $mode){
      $cmd = "timezone";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--rtc|rtc)$/ and not defined $mode){
      $cmd = "rtc";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--clear|clear)$/ and not defined $mode){
      $cmd = "clear";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--show|show)$/ and not defined $mode){
      $cmd = "show";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--buttons|buttons)$/ and not defined $mode){
      $cmd = "buttons";
      $mode = $MODE_CMD;
    }elsif($arg =~ /^(--fill|fill)$/ and @_ > 0 and not defined $mode){
      $cmd = "fill";
      $mode = $MODE_CMD;
      push @cmdParams, "color=" . shift @_;
    }elsif($arg =~ /^--fill=(\w+)$/ and not defined $mode){
      $cmd = "fill";
      $mode = $MODE_CMD;
      push @cmdParams, "color=$1";
    }elsif($arg =~ /^(--lcd|lcd)$/ and @_ > 0 and not defined $mode){
      $cmd = "lcd";
      $mode = $MODE_CMD;
      push @cmdParams, "name=" . shift @_;
    }elsif($arg =~ /^--lcd=(\w+)$/ and not defined $mode){
      $cmd = "lcd";
      $mode = $MODE_CMD;
      push @cmdParams, "name=$1";
    }elsif($arg =~ /^(--orient|orient)$/ and @_ > 0 and not defined $mode){
      $cmd = "orient";
      $mode = $MODE_CMD;
      push @cmdParams, "orient=" . shift @_;
    }elsif($arg =~ /^--orient=(\w+)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "orient";
        $mode = $MODE_CMD;
        push @cmdParams, "orient=$1";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "orient=$1";
      }
    }elsif($arg =~ /^(--framebuf|framebuf)$/ and @_ > 0 and not defined $mode){
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=" . shift @_;
    }elsif($arg =~ /^--framebuf=(.+)$/){
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=$1";
      }elsif($mode eq $MODE_TEXT){
        push @textParams, "framebuf=$1";
      }
    }elsif($arg =~ /^(--upload|upload)$/ and @_ > 0 and not defined $mode){
      $cmd = "upload";
      $mode = $MODE_CMD;
      $cmdFile = shift @_;
      my $destFilename = $cmdFile;
      if(@_ > 0){
        $destFilename = shift @_;
      }
      push @cmdParams, "filename=$destFilename";
    }elsif($arg =~ /^--upload=(.+)$/ and not defined $mode){
      $cmd = "upload";
      $mode = $MODE_CMD;
      $cmdFile = $1;
      my $destFilename = $cmdFile;
      if(@_ > 0){
        $destFilename = shift @_;
      }
      push @cmdParams, "filename=$destFilename";
    }elsif($arg =~ /^(--delete|delete)$/ and @_ > 0 and not defined $mode){
      $cmd = "delete";
      $mode = $MODE_CMD;
      push @cmdParams, "filename=" . shift @_;
    }elsif($arg =~ /^--delete=(.+)$/ and not defined $mode){
      $cmd = "delete";
      $mode = $MODE_CMD;
      push @cmdParams, "filename=" . $1;
    }elsif($arg =~ /^(--bootloader|bootloader|--bootsel|bootsel)$/ and not defined $mode){
      $cmd = "bootloader";
      $mode = $MODE_CMD;
      $curlMaxTime = 3;
    }elsif($arg =~ /^(full|left|right|top|bottom|square)$/ and not defined $mode){
      my $framebufConfName = $1;
      $cmd = "framebuf";
      $mode = $MODE_CMD;
      push @cmdParams, "framebuf=$framebufConfName";
    }elsif($arg =~ /^--(full|left|right|top|bottom|square)$/){
      my $framebufConfName = $1;
      if(not defined $mode or $mode eq $MODE_CMD){
        $cmd = "framebuf";
        $mode = $MODE_CMD;
        push @cmdParams, "framebuf=$framebufConfName";
      }else{
        push @textParams, "framebuf=$framebufConfName";
      }
    }elsif($arg =~ /^(--clear-text)$/){
      $isClearText = 1;
    }elsif($arg =~ /^(--no-clear-text|--no-clear)$/){
      $isClearText = 0;
    }elsif($arg =~ /^--lcd-width=(\d+)$/){
      $lcdWidthPx = $1;
    }elsif($arg =~ /^--lcd-height=(\d+)$/){
      $lcdHeightPx = $1;
    }elsif($arg =~ /^(--lines)$/){
      $splitLines = $SPLIT_LINES_ALWAYS;
    }elsif($arg =~ /^(--no-lines)$/){
      $splitLines = $SPLIT_LINES_NEVER;
    }elsif($arg =~ /^(--lines-big-only)$/){
      $splitLines = $SPLIT_LINES_BIG_ONLY;
    }elsif($arg =~ /^--color=(.+)$/){
      $color = $1;
    }elsif($arg =~ /^--size=(.+)$/){
      $size = $1;
    }elsif($arg =~ /^--hspace=(.+)$/){
      $hspace = $1;
    }elsif($arg =~ /^--vspace=(.+)$/){
      $vspace = $1;
    }elsif($arg =~ /^--x=(.+)$/){
      $x = $1;
    }elsif($arg =~ /^--y=(.+)$/){
      $y = $1;
    }elsif(-f $arg and (not defined $mode or $mode eq $MODE_TEXT)){
      open FH, "< $arg" or die "ERROR: could not read $arg\n$!\n";
      my $contents = join '', <FH>;
      close FH;
      $text .= $contents;
      $mode = $MODE_TEXT;
    }elsif((defined $mode and $mode eq $MODE_TEXT) or ($arg !~ /^-/ and not defined $mode)){
      $text .= " " if $text ne "";
      $text .= $arg;
      $mode = $MODE_TEXT;
    }elsif((defined $cmd and $cmd eq "template") and @cmdParams == 0){
      push @cmdParams, "templateName=$arg";
    }elsif((defined $cmd and $cmd eq "template") and @cmdParams == 1 and not defined $cmdData){
      $cmdData = $arg;
    }elsif((defined $cmd and $cmd eq "timeout") and @cmdParams == 0){
      push @cmdParams, "timeoutMillis=$arg";
    }elsif((defined $cmd and $cmd eq "timezone") and @cmdParams == 0){
      push @cmdParams, "name=$arg";
    }elsif((defined $cmd and $cmd eq "rtc") and @cmdParams == 0){
      push @cmdParams, "epoch=$arg";
    }else{
      die "ERROR: unknown arg $arg\n";
    }
  }

  if(not defined $ipAddr){
    if(not defined $devName){
      die "ERROR: no devices configured\n" if @DEVICE_NAMES == 0;
      $devName = $DEVICE_NAMES[0];
    }

    $ipAddr = $DEVICE_IPS_BY_NAME{lc $devName};
    if(not defined $ipAddr){
      die "ERROR: could not find IP address for $devName in $DEVICE_CONFIG_FILE\n";
    }
  }

  $mode = $MODE_TEXT if not defined $mode;

  my @curlOpts = ("--no-progress-meter");
  @curlOpts = (@curlOpts, "--max-time", $curlMaxTime) if defined $curlMaxTime;

  if($mode eq $MODE_CMD){
    runCurlCmd(\@curlOpts, $ipAddr, $cmd, \@cmdParams, $cmdData, $cmdFile);
  }elsif($mode eq $MODE_TEXT){
    my $charsTotal = length $text;

    my $isLines;
    if($splitLines eq $SPLIT_LINES_ALWAYS){
      $isLines = 1;
    }elsif($splitLines eq $SPLIT_LINES_BIG_ONLY){
      $isLines = $charsTotal > $SPLIT_LINES_BIG_CHAR_LIMIT ? 1 : 0;
    }else{
      $isLines = 0;
    }

    #remove '\r' chars
    $text =~ s/\r\n/\n/g;
    $text =~ s/\r/\n/g;

    #replace '!n!' markup with newlines
    my $newText = "";
    for(my $i=0; $i<length $text; $i++){
      my $ch = substr($text, $i, 1);
      if($ch eq "!"){
        my $cmd = "!";
        $i++;
        while($i < length $text and substr($text, $i, 1) ne "!"){
          $cmd .= substr($text, $i, 1);
          $i++;
        }
        if($i < length $text){
          $cmd .= substr($text, $i, 1);
          die "ERROR: failed to parse $text\n" if $cmd !~ /^![^!]*!$/;
        }else{
          die "ERROR: failed to parse $text\n" if $cmd !~ /^![^!]*$/;
        }
        if($cmd eq "!n!"){
          $newText .= "\n";
        }else{
          $newText .= $cmd;
        }
      }else{
        $newText .= $ch;
      }
    }
    $text = $newText;

    my @lines = split /\n/, $text;

    my $charsH = @lines;
    my $charsW = 0;
    for my $line(@lines){
      $charsW = length $line if length $line > $charsW;
    }
    $charsW = 1 if $charsW <= 1;
    $charsH = 1 if $charsH <= 1;

    $vspace = 1 if not defined $vspace;
    $hspace = 1 if not defined $hspace;

    #select the largest size that will fit all characters,
    #  vertically and horizontally,
    #  or 1 if they cannot fit
    if(not defined $size){
      if(not defined $lcdWidthPx or not defined $lcdHeightPx){
        my $info = `curl @curlOpts 'http://$ipAddr/info'`;
        if($info =~ /window:\s+(\d+)x(\d+)/){
          $lcdWidthPx = $1;
          $lcdHeightPx = $2;
        }else{
          $lcdWidthPx = $DEFAULT_LCD_WIDTH;
          $lcdHeightPx = $DEFAULT_LCD_HEIGHT;
        }
      }

      my $hSize = int($lcdWidthPx / ($charsW*($FONT_BASE_WIDTH + $hspace)));
      my $vSize = int($lcdHeightPx / ($charsH*($FONT_BASE_HEIGHT + $vspace)));
      $size = $hSize < $vSize ? $hSize : $vSize;
      $size = 1 if $size < 1;
    }

    my @messages;
    if($isLines){
      my $height = ($FONT_BASE_HEIGHT+$vspace)*$size;
      my $offset = 0;
      for my $line(@lines){
        push @messages, "!x=0!!y=$offset!$line";
        $offset += $height;
      }
    }else{
      if(length $text > 0){
        @messages = ($text);
      }
    }

    @messages = grep {length $_ > 0} @messages;

    for(my $i=0; $i<@messages; $i++){
      my $msg = $messages[$i];
      $msg = "!size=$size!$msg" if defined $size;
      $msg = "!color=$color!$msg" if defined $color;
      $msg = "!hspace=$hspace!$msg" if defined $hspace and $hspace != 1.0;
      $msg = "!vspace=$vspace!$msg" if defined $vspace and $vspace != 1.0;
      $msg = "!x=$x!$msg" if defined $x;
      $msg = "!y=$y!$msg" if defined $y;

      print "$msg\n" unless $quiet;

      my @params = (@textParams);
      #clear() on the first message unless --no-clear
      if($isClearText and $i == 0){
        @params = (@params, "clear=true");
      }else{
        @params = (@params, "clear=false");
      }

      #send info=true on the last message
      if($i == $#messages){
        @params = (@params, "info=true");
      }

      #do not show() until last message
      if($i < $#messages){
        @params = (@params, "show=false");
      }else{
        @params = (@params, "show=true");
      }

      runCurlCmd(\@curlOpts, $ipAddr, "text", \@params, $msg, undef);
    }
  }elsif($mode eq $MODE_TEMPLATE_SET){
    my $set = $$TEMPLATE_SETS{$templateSet};
    for my $templateName(sort keys %$set){
      my $markup = $$set{$templateName};
      runCurlCmd(\@curlOpts, $ipAddr, "template", ["templateName=$templateName"], $markup, undef);
    }
  }else{
    die "ERROR: unknown mode $mode\n";
  }
}

sub runCurlCmd($$$$$$){
  my ($curlOpts, $ipAddr, $cmd, $cmdParams, $cmdData, $cmdFile) = @_;
  my $paramsFmt = "";
  if(@$cmdParams > 0){
    $paramsFmt = "?" . join "&", @$cmdParams;
  }

  my @curlCmd = ("curl", @$curlOpts, "http://$ipAddr/$cmd$paramsFmt");
  @curlCmd = (@curlCmd, "--data", $cmdData) if defined $cmdData;
  @curlCmd = (@curlCmd, "--upload-file", $cmdFile) if defined $cmdFile;
  system @curlCmd;
}

sub parseConfig($){
  my @devices;
  my @lines = `cat $DEVICE_CONFIG_FILE 2>/dev/null`;
  my $sep = '(?:\s*[=,]\s*)';
  for my $line(@lines){
    if($line =~ /^\s*(\w+)$sep(\d+\.\d+\.\d+\.\d+)(?:$sep(.*))?$/){
      my ($devName, $devIP, $synStr) = ($1, $2, $3);
      $synStr = "" if not defined $synStr;
      my @syns = split /\s+/, $synStr;
      @syns = grep {$_ =~ /^\w+$/} @syns;
      push @devices, {devName => $devName, devIP => $devIP, syns => [@syns]};
    }
  }
  return @devices;
}

&main(@ARGV);
